#!/usr/bin/perl

use strict;
use Getopt::Std;
use Data::Faker;
use Business::CreditCard;
use FS::UID qw(adminsuidsetup);
use FS::Record qw(qsearch);
use FS::cust_main;
use FS::cust_pkg;
use FS::svc_acct;

my $agentnum = 1;
my $refnum = 1;

#my @pkgs = ( 2, 3, 4 );
my @pkgs = ( 4, 5, 6 );
my $svcpart = 2;

use vars qw( $opt_p );
getopts('p:');

my $user = shift or die &usage;
my $num = shift or die &usage;
adminsuidsetup($user);

my $onum = $num;
my $start = time;

until ( $num-- <= 0 ) {

  my $faker = new Data::Faker;

  my $cust_main = new FS::cust_main {
    'agentnum' => $agentnum,
    'refnum'   => $refnum,
    'first'    => $faker->first_name,
    'last'     => $faker->last_name,
    'company'  => ( $num % 2 ? $faker->company. ', '. $faker->company_suffix : '' ), #half with companies..
    'address1' => $faker->street_address,
    'city'     => 'Tofutown', #missing, so everyone is from tofutown# $faker->city,
    'state'    => $faker->us_state_abbr,
    'zip'      => $faker->us_zip_code,
    'country'  => 'US',
    'daytime'  => $faker->phone_number,
    'night'    => $faker->phone_number,
    #forget it, these can have extensions# 'fax'      => ( $num % 2 ? $faker->phone_number : '' ), #ditto
    #bah, forget shipping addresses
    'payby'    => 'BILL',
    'payip'    => $faker->ip_address,
  };

  if ( $opt_p eq 'CARD' || ( !$opt_p && rand() > .33 ) ) {
    $cust_main->payby('CARD');
    my $cardnum = '4123'. sprintf('%011u', int(rand(100000000000)) );
    $cust_main->payinfo( $cardnum. generate_last_digit($cardnum) );
    $cust_main->paydate( '2009-05-01' );
  } elsif ( $opt_p eq 'CHEK' || ( !$opt_p && rand() > .66 ) ) {
    $cust_main->payby('CHEK');
    my $payinfo = sprintf('%7u@%09u', int(rand(10000000)), int(rand(1000000000)) ); 
    $cust_main->payinfo($payinfo);
    $cust_main->payname( 'First International Bank of Testing' );
  }

  # could insert invoicing_list and other stuff too..  hell, could insert
  # packages, services, more
  # but i just wanted 10k customers to test the pager and this was good enough
  # not anymore, here's some services and packages
  
  my $now = time;
  my $year = 31556736; #60*60*24*365.24
  my $setup = $now - int(rand($year));

  my $cust_pkg = new FS::cust_pkg {
    'pkgpart' => $pkgs[ int(rand(scalar(@pkgs))) ],

    #some dates in here would be nice
    'setup'      => $setup,
    #'last_bill'
    #'bill'
    #'susp'
    #'expire'
    #'cancel'
  };

  my $svc_acct = new FS::svc_acct {
    'svcpart'  => $svcpart,
    'username' => $faker->username,
  };

  while ( qsearch( 'svc_acct', { 'username' => $svc_acct->username } ) ) {
    my $username = $svc_acct->username;
    $username++;
    $svc_acct->username($username);
  }

  use Tie::RefHash;
  tie my %hash, 'Tie::RefHash',
    $cust_pkg => [ $svc_acct ],
  ;

  my $error = $cust_main->insert( \%hash );
  die $error if $error;

}

my $end = time;

my $sec = $end-$start;
$sec=1 if $sec==0;
my $persec = $onum / $sec;
print "$onum customers inserted in $sec seconds ($persec customers/sec)\n";

#---

sub usage {
  die "Usage:\n\n  customer-faker [ -p payby ] user num_fakes\n";
}
