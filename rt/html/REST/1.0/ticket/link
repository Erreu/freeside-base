%# BEGIN LICENSE BLOCK
%# 
%# Copyright (c) 1996-2003 Jesse Vincent <jesse@bestpractical.com>
%# 
%# (Except where explictly superceded by other copyright notices)
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# Unless otherwise specified, all modifications, corrections or
%# extensions to this work which alter its source code become the
%# property of Best Practical Solutions, LLC when submitted for
%# inclusion in the work.
%# 
%# 
%# END LICENSE BLOCK
%# REST/1.0/ticket/link
%#
<%ARGS>
$id => undef
$del => 0
$rel
$to
</%ARGS>
<%INIT>
use RT::Interface::REST;

my $output;
my $status = "200 Ok";
my $ticket = new RT::Ticket $session{CurrentUser};
my $object = $r->path_info;

my @fields = qw(DependsOn DependedOnBy RefersTo ReferredToBy HasMember MemberOf);
my %fields = map { lc $_ => $_ } @fields;
my %lfields = (
    HasMember    => { Type => 'MemberOf',  Mode => 'Base'   },
    ReferredToBy => { Type => 'RefersTo',  Mode => 'Base'   },
    DependedOnBy => { Type => 'DependsOn', Mode => 'Base'   },
    MemberOf     => { Type => 'MemberOf',  Mode => 'Target' },
    RefersTo     => { Type => 'RefersTo',  Mode => 'Target' },
    DependsOn    => { Type => 'DependsOn', Mode => 'Target' },
);

# http://.../REST/1.0/ticket/link/1

$object =~ s#^/##;
if ($id && $object && $id != $object) {
    $output = "Different ids in URL (`$object') and submitted form (`$id').\n";
    $status = "400 Bad Request";
    goto OUTPUT;
}
$id ||= $object;
unless ($id =~ /^\d+$/ && $to =~ /^\d+$/) {
    my $bad = ($id !~ /^\d+$/) ? $id : $to;
    $output = $r->path_info. "\n";
    $output .= "Invalid ticket id: '$bad'.\n";
    $status = "400 Bad Request";
    goto OUTPUT;
}
unless (exists $fields{lc $rel}) {
    $output = "Invalid relationship: '$rel'.\n";
    $status = "400 Bad Request";
    goto OUTPUT;
}
$rel = $fields{lc $rel};

$ticket->Load($id);
unless ($ticket->Id) {
    $output = "Couldn't load ticket id: '$id'.\n";
    $status = "404 Ticket not found";
    goto OUTPUT;
}

my $type = $lfields{$rel}->{Type};
my $mode = $lfields{$rel}->{Mode};

my $n = 1;
my $op = $del ? "DeleteLink" : "AddLink";

($n, $output) = $ticket->$op(Type => $type, $mode => $to);
if ($n == 0) {
    $status = "500 Error";
}

OUTPUT:
</%INIT>
RT/<% $RT::VERSION %> <% $status %>

<% $output |n %>
