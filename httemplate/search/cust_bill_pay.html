<% include( 'elements/search.html',
                'title'       => $title,
                'name'        => 'net payments',
                'query'       => $sql_query,
                'count_query' => $count_query,
                'count_addl'  => [ '$%.2f total paid (net)', ],
                'header'      => [ 'Net applied',
                                   'to Invoice',
                                   'Payment',
                                   'By',
                                   FS::UI::Web::cust_header(),
                                 ],
                'fields'      => [
                   sub { $money_char. sprintf('%.2f', shift->amount ) },
                   sub { my $cbp = shift;
                         '#'.$cbp->invnum. ' '.
                         time2str('%b %d %Y', $cbp->cust_bill_date ).
                         " ($money_char".
                           sprintf('%.2f', $cbp->cust_bill_amount).
                         ")" 
                       },
                   sub { my $cbp = shift;
                         $cbp->cust_pay->payby_payinfo_pretty. ' '.
                         time2str('%b %d %Y', $cbp->_date ).
                         " ($money_char".
                         sprintf('%.2f', $cbp->cust_pay_paid ).
                         ")"
                       },
                   sub { shift->cust_pay->otaker },
                   \&FS::UI::Web::cust_fields,
                ],
                'align' => 'rrrl'.FS::UI::Web::cust_aligns(),
                'links' => [
                             '',
                             $cust_bill_link,
                             $cust_pay_link,
                             '',
                             ( map { $_ ne 'Cust. Status' ? $cust_link : '' }
                                   FS::UI::Web::cust_header()
                             ),
                           ],
                'color' => [ 
                             '',
                             '',
                             '',
                             '',
                             FS::UI::Web::cust_colors(),
                           ],
                'style' => [ 
                             '',
                             '',
                             '',
                             '',
                             FS::UI::Web::cust_styles(),
                           ],
          )
%>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('Financial reports');

my $conf = new FS::Conf;
my $money_char = $conf->config('money_char') || '$';

my $title = 'Net Payment Search Results';

my @search = ();

if ( $cgi->param('agentnum') && $cgi->param('agentnum') =~ /^(\d+)$/ ) {
  push @search, "agentnum = $1";
  my $agent = qsearchs('agent', { 'agentnum' => $1 } );
  die "unknown agentnum $1" unless $agent;
  $title = $agent->agent. " $title";
}

my($beginning, $ending) = FS::UI::Web::parse_beginning_ending($cgi);
push @search, "cust_bill._date >= $beginning ",
              "cust_bill._date <= $ending";

#here is the agent virtualization
push @search, $FS::CurrentUser::CurrentUser->agentnums_sql;

my $where = 'WHERE '. join(' AND ', @search);
#
my $count_query = 'SELECT COUNT(*), SUM(amount)
                   FROM cust_bill_pay
                     LEFT JOIN cust_bill USING ( invnum  )
                     LEFT JOIN cust_main USING ( custnum ) '.
                  $where;

my $sql_query   = {
  'table'     => 'cust_bill_pay',
  'select'    => join(', ',
                   'cust_bill_pay.*',
                   'cust_pay.paid       AS cust_pay_paid',
                   'cust_bill._date     AS cust_bill_date',
                   #'cust_bill.charged   AS cust_bill_charged',
                   'cust_pay.custnum    AS custnum',
                   'cust_main.custnum   AS cust_main_custnum',
                   FS::UI::Web::cust_sql_fields(),
                 ),
  'hashref'   => {},
  'extra_sql' => $where,
  'addl_from' => 'LEFT JOIN cust_bill   USING ( invnum  )
                  LEFT JOIN cust_pay    USING ( paynum )
                  LEFT JOIN cust_main ON ( cust_bill.custnum = cust_main.custnum )',
};

my $cust_bill_link = sub {
  my $cust_bill_pay = shift;
  $cust_bill_pay->invnum
    ? [ "${p}view/cust_bill.cgi?", 'invnum' ]
    : '';
};

my $cust_pay_link = sub {
  my $cust_bill_pay = shift;
  $cust_bill_pay->paynum
    ? [ "${p}view/cust_pay.html?paynum=", 'paynum' ]
    : '';
};

my $cust_link = sub {
  my $cust_credit_bill = shift;
  $cust_credit_bill->cust_main_custnum
    ? [ "${p}view/cust_main.cgi?", 'cust_main_custnum' ]
    : '';
};

</%init>
