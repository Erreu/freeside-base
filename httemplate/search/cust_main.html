<% include( 'elements/search.html',
                  'title'       => 'Customer Search Results', 
                  'menubar'     => $menubar,
                  'name'        => 'customers',
                  'query'       => $sql_query,
                  'count_query' => $count_query,
                  'header'      => [ FS::UI::Web::cust_header(
                                       $cgi->param('cust_fields')
                                     ),
                                     @extra_headers,
                                   ],
                  'fields'      => [
                    \&FS::UI::Web::cust_fields,
                    @extra_fields,
                  ],
                  'color'       => [ FS::UI::Web::cust_colors(),
                                     map '', @extra_fields
                                   ],
                  'style'       => [ FS::UI::Web::cust_styles(),
                                     map '', @extra_fields
                                   ],
                  'align'       => [ FS::UI::Web::cust_aligns(),
                                     map '', @extra_fields
                                   ],
                  'links'       => [ ( map { $_ ne 'Cust. Status' ? $link : '' }
                                           FS::UI::Web::cust_header(
                                                      $cgi->param('cust_fields')
                                                                   )
                                     ),
                                     map '', @extra_fields
                                   ],
              )
%>
<%init>

die "access denied"
  unless ( $FS::CurrentUser::CurrentUser->access_right('List customers') &&
           $FS::CurrentUser::CurrentUser->access_right('List packages')
         );

my %search_hash = ();

#$search_hash{'query'} = $cgi->keywords;

#scalars
my @scalars = qw (
  agentnum status cancelled_pkgs cust_fields flattened_pkgs custbatch
  no_censustract
);

for my $param ( @scalars ) {
  $search_hash{$param} = scalar( $cgi->param($param) )
    if $cgi->param($param);
}

#lists
for my $param (qw( payby )) {
  $search_hash{$param} = [ $cgi->param($param) ]
    if $cgi->param($param);
}

###
# parse dates
###

foreach my $field (qw( signupdate )) {

  my($beginning, $ending) = FS::UI::Web::parse_beginning_ending($cgi, $field);

  next if $beginning == 0 && $ending == 4294967295;
       #or $disable{$cgi->param('status')}->{$field};

  $search_hash{$field} = [ $beginning, $ending ];

}

##
# amounts
##

$search_hash{'current_balance'} =
  [ FS::UI::Web::parse_lt_gt($cgi, 'current_balance') ];

###
# etc
###

my $sql_query = FS::cust_main->search_sql(\%search_hash);
my $count_query   = delete($sql_query->{'count_query'});
my @extra_headers = @{ delete($sql_query->{'extra_headers'}) };
my @extra_fields  = @{ delete($sql_query->{'extra_fields'})  };

my $link = [ "${p}view/cust_main.cgi?", 'custnum' ];

###
# email links
###

my $menubar = [];

if ( $FS::CurrentUser::CurrentUser->access_right('Bulk send customer notices') ) {

  my $uri = new URI;
  $uri->query_form( \%search_hash );
  my $query = $uri->query;

  push @$menubar, 'Email a notice to these customers' =>
                    "${p}misc/email-customers.html?$query",

}

</%init>
