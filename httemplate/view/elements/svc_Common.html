%  # options example...
%  #
%  # 'table' => 'svc_something'
%  #
%  # 'labels' => {
%  #               'column' => 'Label',
%  #             },
%  #
%  # listref - each item is a literal column name (or method) or (notyet) coderef
%  # if not specified all columns (except for the primary key) will be viewable
%  # 'fields' => [
%  #             ]
%  #
%  # # defaults to "edit/$table.cgi?", will have svcnum appended
%  # 'edit_url' => 
%
%
% if ( $custnum ) { 


  <% include("/elements/header.html","View $label: $value") %>

  <% include( '/elements/small_custview.html', $custnum, '', 1,
     "${p}view/cust_main.cgi") %>
  <BR>
% } else { 


  <SCRIPT>
  function areyousure(href) {
      if (confirm("Permanently delete this <% $label %>?") == true)
          window.location.href = href;
  }
  </SCRIPT>

  <% include("/elements/header.html","View $label: $value", menubar(
      "Cancel this (unaudited) $label" =>
            "javascript:areyousure(\'${p}misc/cancel-unaudited.cgi?$svcnum\')"
  )) %>
% } 


Service #<B><% $svcnum %></B>
% my $url = $opt{'edit_url'} || $p. 'edit/'. $opt{'table'}. '.cgi?';
| <A HREF="<%$url%><%$svcnum%>">Edit this <% $label %></A>
<BR>

<% ntable("#cccccc") %><TR><TD><% ntable("#cccccc",2) %>
% foreach my $f ( @$fields ) {
%
%     my( $field, $type);
%     if ( ref($f) ) {
%       $field = $f->{'field'},
%       $type  = $f->{'type'} || 'text',
%     } else {
%       $field = $f;
%       $type = 'text';
%     }
%


  <TR>
    <TD ALIGN="right">
      <% ( $opt{labels} && exists $opt{labels}->{$field} )
              ? $opt{labels}->{$field}
              : $field
      %>
    </TD>
%
%      #eventually more options for <SELECT>, etc. fields
%    


    <TD BGCOLOR="#ffffff"><% $svc_x->$field %><TD>

  </TR>
% } 
% foreach (sort { $a cmp $b } $svc_x->virtual_fields) { 

  <% $svc_x->pvf($_)->widget('HTML', 'view', $svc_x->getfield($_)) %>
% } 


</TABLE></TD></TR></TABLE>

<BR>
<% joblisting({'svcnum'=>$svcnum}, 1) %>

<% include('/elements/footer.html') %>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('View customer services')
      || $FS::CurrentUser::CurrentUser->access_right('View customer'); #XXX remove me

my(%opt) = @_;

my $table = $opt{'table'};

my $fields = $opt{'fields'}
             #|| [ grep { $_ ne 'svcnum' } dbdef->table($table)->columns ];
             || [ grep { $_ ne 'svcnum' } fields($table) ];

my $svcnum;
if ( $cgi->param('svcnum') ) {
  $cgi->param('svcnum') =~ /^(\d+)$/ or die "unparsable svcnum";
  $svcnum = $1;
} else {
  my($query) = $cgi->keywords;
  $query =~ /^(\d+)$/ or die "no svcnum";
  $svcnum = $1;
}
my $svc_x = qsearchs({
  'select'    => $opt{'table'}.'.*',
  'table'     => $opt{'table'},
  'addl_from' => ' LEFT JOIN cust_svc  USING ( svcnum  ) '.
                 ' LEFT JOIN cust_pkg  USING ( pkgnum  ) '.
                 ' LEFT JOIN cust_main USING ( custnum ) ',
  'hashref'   => { 'svcnum' => $svcnum },
  'extra_sql' => ' AND '. $FS::CurrentUser::CurrentUser->agentnums_sql,
}) or die "Unknown svcnum $svcnum in ". $opt{'table'}. " table\n";

my $cust_svc = $svc_x->cust_svc;
my($label, $value, $svcdb) = $cust_svc->label;

my $pkgnum = $cust_svc->pkgnum;

my($cust_pkg, $custnum);
if ($pkgnum) {
  $cust_pkg = $cust_svc->cust_pkg;
  $custnum = $cust_pkg->custnum;
} else {
  $cust_pkg = '';
  $custnum = '';
}

</%init>
